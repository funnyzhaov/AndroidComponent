if(isBuildModule.toBoolean()){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
}
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        if (isBuildModule.toBoolean()) {
            applicationId "com.xuexibaoapp.educhain.login"
            versionCode 1
            versionName "1.0"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : "w_login" ]
            }
        }


    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets{
        main{
            if(isBuildModule.toBoolean()){
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }else{
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java{
                    //排除debug文件下的所有java文件
                    exclude 'debug/**'
                }
            }
        }
    }

    //资源冲突,不同组件下设置不同的资源前缀
    resourcePrefix "w_login_"

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    annotationProcessor "com.alibaba:arouter-compiler:${rootProject.ext.arouterCompiler}"
    compile project(':componentservice')
}
